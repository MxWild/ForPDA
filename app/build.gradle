apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    def versionNumber = 32

    def versionMajor = 0
    def versionMinor = 3
    def versionPatch = 7

    def versionBuild = -1
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        applicationId "ru.forpdateam.forpda"
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
        minSdkVersion 17
        targetSdkVersion 25
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    productFlavors {
        stable {}
        beta {
            applicationId 'ru.forpdateam.forpda.beta'
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild}) beta"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileNaming = "apk/ForPDA"
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(getProject().getRootDir(), "${fileNaming}-${versionMajor}.${versionMinor}.${versionPatch}-${outputFile.name}")
            }
        }
    }


//    dexOptions {
//        dexInProcess = true
//    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
    }
    splits {
        abi {
            enable gradle.startParameter.taskNames.contains(":app:assembleRelease")
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }
    lintOptions {
        disable 'RestrictedApi'
    }
    sourceSets {
        main {
            java {
                srcDirs += 'src/main/java'
                srcDirs += 'src/main/kotlin'
                srcDirs += 'build/generated-src/kotlin'
            }
            res {
                srcDirs =
                        [
                        'src/main/res/layouts/other',
                        'src/main/res/layouts/drawer',
                        'src/main/res/layouts/base',
                        'src/main/res/layouts/forum',
                        'src/main/res/layouts/auth',
                        'src/main/res/layouts/imgviewer',
                        'src/main/res/layouts/reputation',
                        'src/main/res/layouts/activitys',
                        'src/main/res/layouts/panel',
                        'src/main/res/layouts/search',
                        'src/main/res/layouts/profile',
                        'src/main/res/layouts/qms',
                        'src/main/res/layouts/theme',
                        'src/main/res/layouts/fortest',
                        'src/main/res/layouts/topic',
                        'src/main/res/layouts/news',
                        'src/main/res/layouts',
                        'src/main/res'
                        ]
            }
        }
    }
}

ext {
    supportLibVersion = '25.3.1'
    okHttpVersion = '3.8.1'
    rxCoreVersion = '2.1.0'
    rxAndroidVersion = '2.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:palette-v7:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:preference-v7:${supportLibVersion}"
    compile 'com.google.android:flexbox:0.3.0'
    compile "android.arch.lifecycle:runtime:1.0.0-alpha4"
    compile "android.arch.lifecycle:extensions:1.0.0-alpha4"
    compile "android.arch.persistence.room:runtime:1.0.0-alpha4"
    compile "android.arch.persistence.room:rxjava2:1.0.0-alpha4"
    kapt "android.arch.lifecycle:compiler:1.0.0-alpha4"
    kapt "android.arch.persistence.room:compiler:1.0.0-alpha4"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:okhttp-urlconnection:${okHttpVersion}"
    compile "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    compile "io.reactivex.rxjava2:rxjava:${rxCoreVersion}"
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile group: 'org.ccil.cowan.tagsoup', name: 'tagsoup', version: '1.2'
    compile 'org.bsc.util:minitemplator-repackaged:1.2'
    compile 'com.lapism:searchview:4.0'
    compile 'com.github.bmelnychuk:atv:1.2.9'
    compile 'com.afollestad:sectioned-recyclerview:0.2.3'
    compile 'com.faendir:acra:4.10.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.wada811:android-material-design-colors:3.0.0'
    compile 'com.thebluealliance:spectrum:0.7.1'
    compile 'com.github.douglasjunior:android-simple-tooltip:0.2.0'


    debugCompile 'com.readystatesoftware.chuck:library:1.0.4'
    releaseCompile 'com.readystatesoftware.chuck:library-no-op:1.0.4'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
